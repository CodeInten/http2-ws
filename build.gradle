plugins {
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.6.3'
}

apply plugin: 'idea'
apply plugin: 'java'

group = 'codeinten.github.io'
version = '0.1-SNAPSHOT'

configurations {
    acceptanceCompile.extendsFrom testCompile
    acceptanceRuntime.extendsFrom testRuntime
}

sourceSets {
    acceptance {
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
}

task copyRuntimeDependencies(type: Copy) {
    from configurations.runtime
    into 'build/runtime/'
}

task acceptance(type: Test) {
    dependsOn(test, copyRuntimeDependencies)
    testClassesDir = sourceSets.acceptance.output.classesDir
    classpath = sourceSets.acceptance.runtimeClasspath
}


check {
    dependsOn(acceptance)
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

repositories {
    mavenCentral()

    maven { url "http://repo1.maven.org/maven2" }
}

dependencies {
    compile group: 'com.twitter', name: 'hpack', version: '1.0.2'
    runtime group: 'com.twitter', name: 'hpack', version: '1.0.2'

    testCompile group: 'junit', name: 'junit', version:'4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'

    acceptanceCompile group: 'de.bechte.junit', name: 'junit-hierarchicalcontextrunner', version:'4.12.1'
    acceptanceCompile group: 'org.eclipse.jetty', name: 'jetty-client', version: '9.3.12.v20160915'
    acceptanceCompile group: 'org.eclipse.jetty.http2', name: 'http2-client', version: '9.3.12.v20160915'
    acceptanceCompile group: 'org.eclipse.jetty.http2', name: 'http2-http-client-transport', version: '9.3.12.v20160915'
}

if (System.env.TRAVIS == 'true') {
    allprojects {
        tasks.withType(GroovyCompile) {
            groovyOptions.fork = false
        }
        tasks.withType(Test) {
            maxParallelForks = 2
            minHeapSize = '128m'
        }
    }
}
